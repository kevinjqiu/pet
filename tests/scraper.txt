# Scraper and rsyncd.

# Correct Configuration.
clear
load 1m
    up{container="scraper", machine="a", experiment="npad", rsync_module="sidestream"} 1
    up{container="scraper", machine="a", experiment="npad", rsync_module="npad"} 1
    up{container="scraper", machine="b", experiment="npad", rsync_module="sidestream"} 1
    up{container="scraper", machine="b", experiment="npad", rsync_module="npad"} 1
    up{service="rsyncd", machine="a", experiment="npad"} 1
    up{service="rsyncd", machine="b", experiment="npad"} 1

# The number of targets per machine matches the number of rsync_modules globally.
eval instant at 0m count BY(machine) (up{container="scraper"}) - scalar( count( count BY(rsync_module) (up{container="scraper"}))) != 0
# EMPTY RESULT.

# The set of monitored scraper targets that are on inventory machines.
eval instant at 0m up{container="scraper"} UNLESS ON(machine, experiment) up{service="rsyncd"}
# EMPTY RESULT.

# The set of rsync inventory that do not have scraper services.
eval instant at 0m up{service="rsyncd"} UNLESS ON(machine, experiment) up{container="scraper"}
# EMPTY RESULT.

# Incorrect Configuration.
clear
load 1m
    up{container="scraper", machine="a", experiment="npad", rsync_module="sidestream"} 1
    up{container="scraper", machine="a", experiment="npad", rsync_module="npad"} 1
    up{container="scraper", machine="c", experiment="npad", rsync_module="sidestream"} 1
    up{container="scraper", machine="c", experiment="npad", rsync_module="npad"} 1
    up{container="scraper", machine="c", experiment="npad", rsync_module="paris-traceroute"} 1

# Machine "a" does not have three rsync_module targets.
eval instant at 0m count BY(machine) (up{container="scraper"}) - scalar( count( count BY(rsync_module) (up{container="scraper"}))) != 0
    {machine="a"} -1


clear
load 1m
    up{container="scraper", machine="a", experiment="npad", rsync_module="sidestream"} 1
    up{container="scraper", machine="a", experiment="npad", rsync_module="npad"} 1
    up{container="scraper", machine="c", experiment="npad", rsync_module="sidestream"} 1
    up{container="scraper", machine="c", experiment="npad", rsync_module="npad"} 1
    up{service="rsyncd", machine="a", experiment="npad"} 1
    up{service="rsyncd", machine="b", experiment="npad"} 1

# Scrapers are configured on machine "c", but machine "c" is not in the rsyncd inventory.
eval instant at 0m up{container="scraper"} UNLESS ON(machine, experiment) up{service="rsyncd"}
    up{container="scraper", machine="c", experiment="npad", rsync_module="sidestream"} 1
    up{container="scraper", machine="c", experiment="npad", rsync_module="npad"} 1

# Rsync inventory includes machine "b", but machine "b" does not have a configured scraper.
eval instant at 0m up{service="rsyncd"} UNLESS ON(machine, experiment) up{container="scraper"}
    up{service="rsyncd", machine="b", experiment="npad"} 1
